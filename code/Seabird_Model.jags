
model {
  
  # --------------------------------
  # Colony-level intercepts (note that GAM structure includes intercept for colony 1)
  # --------------------------------
  
  C[1] <- 0
  for(k in 2:ncolony){
    C[k] ~ dnorm(0,0.01) 
  }
  
  # --------------------------------
  # GAMM smooth
  # --------------------------------
  
  # Describes the variance among colonies in the shapes of their GAMs
 
  sdbeta ~ dunif(0,5) # Prediction (if we understand the model): puffins should have higher sdbeta than petrels
  taubeta <- pow(sdbeta,-2) 
  
  nk1 <- nknots-1
  nk2 <- ((nknots*2)-2)
  B.X[1] ~ dnorm(0,0.01)
  
  ## prior for s(year)... 
  K1 <- S1[1:nk1,1:nk1] * lambda[1]  + S1[1:nk1,(nknots:nk2)] * lambda[2]
  B.X[(2:nknots)] ~ dmnorm(zero[(2:nknots)],K1) 
  
  #K1 is the prior on the precisions of the mnorm B.X values (mean GAM parameters for a species)
  ## smoothing parameter
  
  for(i in 1:2) {
    lambda[i] ~ dgamma(0.05,0.005) # Prediction: could control wiggliness by changing this prior
    rho[i] <- log(lambda[i])
  } # i
  
  for(j in 1:nknots){ # Computation of GAM components
    
    for(k in 1:ncolony){
      
      # Draw betas for each colony (for this knot) from a shared distribution
      beta.X[k,j] ~ dnorm(B.X[j],taubeta) 
      
      for ( i in 1:nyearspred ){
        
        X.part[i,j,k] <- beta.X[k,j]*(X[i,j])
        
      } # i
    } # k
  } # j
  
  # --------------------------------
  # Colony-level dynamics
  # --------------------------------
  
  # Prior for annual process variation
  ProcVar_prior_sd <- 0.2
  ProcVar_prior_var <- pow(ProcVar_prior_sd,2)

  ProcVar_sd ~ dlnorm(log(0.2) - 0.5*ProcVar_prior_var,1/ProcVar_prior_var)
  ProcVar_tau <- pow(ProcVar_sd,-2)
  
  for (i in 1:nyearspred){
    for(k in 1:ncolony){
      
      yeareffect[i,k] <- sum(X.part[i,1:nknots,k])
      log_mu[i,k] ~ dnorm(C[k] + yeareffect[i,k],ProcVar_tau)

    } # k
  } # i

  # --------------------------------
  # Observation process
  # --------------------------------
  
  # priors on SE parameters
  intercept_SE ~ dnorm(0,0.1)
  slope_SE ~ dnorm(0,0.1)
  sd_SE ~ dunif(0,5)
  tau_SE <- pow(sd_SE,-2)
  
  for (i in 1:ncounts) { 
    
    # Filling in missing observation error for certain surveys
    survey_SE[i] ~ dlnorm(intercept_SE + slope_SE * log(survey_count[i]),tau_SE)
    survey_tau[i] <- pow(survey_SE[i],-2)
    
    lambda_count[i] ~ dnorm(exp(log_mu[year[i],colony[i]]), survey_tau[i])    
    count[i] ~ dpois(lambda_count[i])                     
  }
  
  # --------------------------------
  # Derived parameters: predictions of annual expected counts
  # --------------------------------
  
  for (i in 1:nyearspred){
    for(k in 1:ncolony){
      population_index[i,k] <- exp(C[k] + yeareffect[i,k] + 0.5*pow(ProcVar_sd,2))
    }
  } #i

  # --------------------------------
  # Goodness-of-fit evaluation (Posterior predictive check)
  # Simulate new datasets under the fitted model
  # Compare RMSE of simulated datasets to RMSE of empirical data
  # --------------------------------
  
  for (i in 1:ncounts) { 
    
    # Simulate new lambda and new counts under fitted model
    simulated_lambda[i] ~ dnorm(exp(log_mu[year[i],colony[i]]), survey_tau[i])    

    # Calculate discrepancy measures (squared error) for actual and simulated data
    sqE_actual[i] <- pow(lambda_count[i] - population_index[year[i],colony[i]] ,2)
    sqE_simulated[i] <- pow(simulated_lambda[i] - population_index[year[i],colony[i]],2)
     
      
  }

  # Overall root mean squared error of empirical data
  RMSE_actual <- sqrt(mean(sqE_actual[]))
  
  # Overall root mean squared error of simulated data
  RMSE_simulated <- sqrt(mean(sqE_simulated[]))

  
}

